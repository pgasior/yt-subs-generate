plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'
    id 'edu.sc.seis.launch4j' version '2.5.0'
    id "com.github.ben-manes.versions" version '0.38.0'
}

group 'pl.gasior'
version '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'pl.gasior.Main'
    }
}

//shadowJar{
//    minimize {
//        exclude(dependency("org.jetbrains.kotlin:.*:.*"))
//        exclude(dependency("com.rometools:.*:.*"))
//        exclude(dependency("org.slf4j:.*:.*"))
//        exclude(dependency("ch.qos.logback:.*:.*"))
//    }
//}

launch4j {
    mainClassName = 'pl.gasior.Main'
    jvmOptions = ["-Dfile.encoding=UTF-8"]
    copyConfigurable = []
    jarTask = project.tasks.shadowJar
    headerType = "console"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = 11
    }
}

repositories {
    mavenCentral()
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    checkForGradleUpdate = true
    revision = 'release'
    gradleReleaseChannel = 'current'

    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev20210410-1.31.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.31.5'

    implementation 'com.rometools:rome-opml:1.15.0'
    implementation 'com.rometools:rome:1.15.0'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.1'

    implementation 'org.jsoup:jsoup:1.13.1'

    implementation 'com.uchuhimo:konf:1.1.2'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.3"
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.12.3'
    implementation 'com.fasterxml.woodstox:woodstox-core:6.2.5'


    testImplementation 'junit:junit:4.13.2'
}
