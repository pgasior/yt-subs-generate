plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'edu.sc.seis.launch4j' version '2.4.9'
    id "com.github.ben-manes.versions" version '0.36.0'
}

group 'pl.gasior'
version '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

shadowJar{
    minimize {
        exclude(dependency("org.jetbrains.kotlin:.*:.*"))
        exclude(dependency("com.rometools:.*:.*"))
        exclude(dependency("org.slf4j:.*:.*"))
        exclude(dependency("ch.qos.logback:.*:.*"))
    }
}

launch4j {
    mainClassName = 'pl.gasior.Main'
    jvmOptions = ["-Dfile.encoding=UTF-8"]
    copyConfigurable = []
    jar = "$buildDir/libs/${project.tasks.shadowJar.archiveFileName.get()}"
    headerType = "console"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = 11
    }
}

repositories {
    mavenCentral()
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    checkForGradleUpdate = true
    // Example 2: disallow release candidates as upgradable versions from stable versions
    rejectVersionIf {
        isNonStable(candidate.version) && !isNonStable(currentVersion)
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev20200618-1.30.9'
    implementation 'com.google.api-client:google-api-client-java6:1.31.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.31.2'

    implementation 'com.rometools:rome-opml:1.15.0'
    implementation 'com.rometools:rome:1.15.0'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.9.0'

    implementation 'org.jsoup:jsoup:1.13.1'

    implementation 'com.uchuhimo:konf:0.23.0'


    testImplementation 'junit:junit:4.12'
}
